//
// Copyright (C) 2020 Signal Messenger, LLC.
// All rights reserved.
//
// SPDX-License-Identifier: GPL-3.0-only
//

plugins {
    id "de.undercouch.download" version "4.1.1"
}

apply plugin: 'java'
apply from: '../../deploy.gradle'

archivesBaseName = 'zkgroup-java'

sourceCompatibility = 1.7

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.mockito:mockito-core:1.9.5'
    testImplementation 'junit:junit:4.12'
}

sourceSets {
    otherPlatformLibraries {}
}

tasks.withType(JavaCompile) {
    compileTask -> compileTask.dependsOn ':makeServer'
}

tasks.withType(AbstractArchiveTask) {
    preserveFileTimestamps = false
    reproducibleFileOrder = true
}

jar {
    dependsOn ':makeServer'
}

test {
    // Test against the .jar file from build/libs
    dependsOn 'jar'
    classpath = sourceSets.test.runtimeClasspath - files("build/classes/java/main") - files("build/resources/main") + fileTree(dir:"build/libs")
}

task packageJavadoc(type: Jar, dependsOn: 'javadoc') {
    from javadoc.destinationDir
    classifier = 'javadoc'
}

task packageSources(type: Jar) {
    from sourceSets.main.allSource
    classifier = 'sources'
}


task testableJar(type: Jar) {
    from sourceSets.main.output
    from sourceSets.otherPlatformLibraries.resources
    classifier = 'testable'

    def extraResources = ['libzkgroup.dll', 'libzkgroup.dylib']
    def extraResourcesDir = 'src/otherPlatformLibraries/resources'

    doFirst {
        mkdir extraResourcesDir
        extraResources.each { name ->
            def artifact = name.replace('libzkgroup', 'libzkgroup-x64')
            download {
                src 'https://github.com/signalapp/zkgroup/releases/download/v' + project.version + '/' + artifact
                dest extraResourcesDir + '/' + name
            }
        }
    }
}

artifacts {
    archives packageJavadoc
    archives packageSources
    archives testableJar
}
